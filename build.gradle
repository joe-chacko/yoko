/*
 * Copyright 2026 IBM Corporation and others.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an \"AS IS\" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

import groovy.time.TimeCategory
import groovy.time.TimeDuration

import java.time.ZoneOffset

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
  }
}

plugins {
  id "biz.aQute.bnd.builder" version "6.4.0" apply false
}

ext {
  testCount = 0
  successfulTestCount = 0
  failedTestCount = 0
  skippedTestCount = 0
  time = new TimeDuration(0,0,0,0)
  testsResults = []
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

// Apply release configuration
apply from: 'build-release.gradle'

// compute git hash

import static java.time.format.DateTimeFormatter.BASIC_ISO_DATE
import static java.time.format.DateTimeFormatter.ISO_OFFSET_DATE_TIME

def gitHash = 'git rev-parse HEAD'.execute().text.trim()
// compute git commit date
def gitIsoDate = 'git log -1 --date=iso-strict --pretty=format:%cd'.execute().text.trim()
// convert from author's time zone to UTC
def gitIsoDateUtc = ISO_OFFSET_DATE_TIME.parse(gitIsoDate, Instant::from).atZone(ZoneOffset.UTC)
def gitBasicDateUtc = BASIC_ISO_DATE.format(gitIsoDateUtc)
// Although SemVer2.0 prescribes '+' and '-' separators after major.minor.patch
// OSGi mandates major.minor.patch.qualifier
// OSGi permits '-' and '_' within the qualifier, but not at either end
// OSGi permits no other punctuation or whitespace
// Set the version to major.minor.patch.YYYYMMDD_GITHASH
def yokoVersion = "1.5.2.${gitBasicDateUtc.take(8)}_${gitHash.take(10)}"

// set system properties for use in bundle metadata
System.setProperty("git.hash", gitHash);
System.setProperty("git.date", gitIsoDateUtc.toString());

task getVersion {
  printf "Yoko version = %s%n", yokoVersion
}

// These are the production projects.
// Note the ringer: jdk-supplement is an unshipped prereq to allow compiling for Java 8 and running with Java 11.
def productionProjectPaths = [':jdk-supplement', ':yoko-osgi', ':yoko-spec-corba', ':yoko-rmi-spec', ':yoko-util', ':yoko-rmi-impl', ':yoko-core']

/* Standard configuration for each subproject */
subprojects { sp ->
  apply plugin: 'java-library'

  // Production projects MUST compile for Java 8
  // Test projects are now Java 11 and up only.
  sourceCompatibility = targetCompatibility = productionProjectPaths.contains(sp.path) ? 1.8 : 11

  // Use Java 11 for all unit test compilation too
  tasks.withType(JavaCompile).configureEach {if (name.toLowerCase().contains('test')) sourceCompatibility = targetCompatibility = 11 }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  configurations {
    // declare a test library configuration
    testLib
    testImplementation.extendsFrom testLib
  }

  // additional configuration for projects that have bnd.bnd files
  if (sp.file("bnd.bnd").exists()) {
    apply plugin: 'biz.aQute.bnd.builder'

    // to extend the project with a new property, use project.ext.<new property name>
    project.ext.symbolicName = project.name.replaceFirst("^yoko-", symbolicNamePrefix)
    version = yokoVersion
  }

  // standardise the test deps here
  dependencies {
    testLib 'junit:junit:4.12'
    testLib 'org.mockito:mockito-core:2.22.0'
    testLib 'org.mockito:mockito-junit-jupiter:2.22.0'
    testLib "org.hamcrest:hamcrest:2.1"
    testLib "org.junit.jupiter:junit-jupiter:5.9.0"
    testLib "org.junit.platform:junit-platform-runner:1.8.2"
    testLib "org.junit-pioneer:junit-pioneer:1.9.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.9.0"
  }

  if (JavaVersion.current() < JavaVersion.VERSION_11) {
    throw new GradleException("This build must be run with java 11 or higher")
  }

  // ensure tests run under $buildDir somewhere
  test {
    jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
    jvmArgs '--add-opens=java.base/java.io=ALL-UNNAMED'
    jvmArgs '--add-opens=java.base/java.util=ALL-UNNAMED'
    jvmArgs '--add-opens=java.rmi/java.rmi=ALL-UNNAMED'
    useJUnitPlatform()
    workingDir = "$buildDir/testWorkingDir"
    doFirst {workingDir.mkdirs()}

    testLogging {
      // set options for log level LIFECYCLE
      events "skipped", "failed", "passed", "standardOut"
      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true

      // set options for log level DEBUG and INFO
      debug {
        events "skipped", "failed", "started", "passed", "standardOut", "standardError"
        exceptionFormat "full"
      }
      info.events = debug.events
      info.exceptionFormat = debug.exceptionFormat
      ignoreFailures = true // Always try to run all tests for all modules

      afterTest { desc, result ->
        def totalTime = result.endTime - result.startTime
        println """"Test elapsed time","$desc.name",$totalTime"""
      }

      afterSuite { desc, result ->
        if (!desc.parent) { // will match the outermost suite
          def timeTaken = TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))
          String summary = "${desc.name}" +
                  "\n" +
                  "Results: ${result.resultType} " +
                  "(" +
                  "${result.testCount} tests, " +
                  "${result.successfulTestCount} successes, " +
                  "${result.failedTestCount} failures, " +
                  "${result.skippedTestCount} skipped" +
                  ") " +
                  "in ${timeTaken}" +
                  "\n"
          rootProject.testCount += result.testCount
          rootProject.successfulTestCount += result.successfulTestCount
          rootProject.failedTestCount += result.failedTestCount
          rootProject.skippedTestCount += result.skippedTestCount
          rootProject.time += timeTaken

          // Add reports in `testsResults`, keep failed suites at the end
          if (result.resultType == TestResult.ResultType.SUCCESS) {
            rootProject.testsResults.add(0, summary)
          } else {
            rootProject.testsResults += summary
          }

          def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
          def startItem = '|  ', endItem = '  |'
          def repeatLength = startItem.length() + output.length() + endItem.length()
          println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
        }
        def totalTime = result.endTime - result.startTime
        println """"Suite elapsed time","$desc.name",$totalTime"""
      }
    }

    reports {
      junitXml.enabled = true
      html.enabled = true
    }
  }
}

gradle.buildFinished {
  String overallSummary = "Total Summary" +
          "\n" +
          "Results: ${(failedTestCount == 0) ? "SUCCESS": "FAILURE"} " +
          "(" +
          "${rootProject.testCount} tests, " +
          "${rootProject.successfulTestCount} successes, " +
          "${rootProject.failedTestCount} failures, " +
          "${rootProject.skippedTestCount} skipped" +
          ") " +
          "in ${rootProject.time}" +
          "\n"
  rootProject.testsResults.add(overallSummary)

  def allResults = rootProject.testsResults
  if (!allResults.isEmpty()) {
    printResults rootProject.testsResults
  }
}

private void printResults(allResults) {
  if (logger.isQuietEnabled()) return
  // Max line length has to take lines from all items into account
  def maxLength = allResults*.readLines().flatten().collect { it.length() }.max()

  println "\n  \n Build Summary:"
  println ",${"${"-" * maxLength}"}." // Top border

  println allResults.collect {
    it.readLines().collect { // Add left and right border to each _line_
      "|" + it + " " * (maxLength - it.length()) + "|"
    }.join("\n")
  }.join("\n+${"${"-" * maxLength}"}+\n") // Add separator between entries

  println "`${"${"-" * maxLength}"}'" // Print bottom border
}

/* Standard configuration for subprojects with shippable outputs */
configure([
  project(':yoko-osgi'),
  project(':yoko-util'),
  project(':yoko-spec-corba'),
  project(':yoko-rmi-spec'),
  project(':yoko-rmi-impl'),
  project(':yoko-core'),
  project(':testify'),
]) {
  apply plugin: 'maven-publish'
  publishing {
    publications {
      maven(MavenPublication) {
        artifact jar

        pom.withXml {
          def rootNode = asNode()

          // // specify packaging type as bundle (default is pom)
          // def packaging = root.packaging[0] ?:
          //                 root.appendNode('packaging')
          // packaging.setValue('bundle')

          def depsNode = rootNode.dependencies[0] ?:
                  rootNode.appendNode('dependencies')

          // declare compile scope dependencies in the pom.xml
          // as compile dependencies
          configurations.implementation.allDependencies.each {
            def depNode = depsNode.appendNode('dependency')
            depNode.appendNode('groupId', it.group)
            depNode.appendNode('artifactId', it.name)
            depNode.appendNode('version', it.version)
            depNode.appendNode('scope', 'compile')
          }
        }
      }
    }
  }

  jar {
    dependsOn "generatePomFileForMavenPublication"
    // generate the maven dependency metadata
    into("/META-INF/maven/$project.group/$project.name") {
      from 'build/publications/maven'
      rename "pom-default.xml", "pom.xml"
    }
  }

  publish.dependsOn build
  // generated tasks need to be expressed as strings
  // because they don't exist yet when these lines are executed
  publish.dependsOn "publishMavenPublicationToMavenLocal"
  publish.dependsOn "publishToMavenLocal"
}

task testReport (type: TestReport) {
    // GitHub artifact uploader won't upload anything in the root project. Not sure why.
    // Generate the aggregate report into a subproject
    destinationDir = file("${buildDir}/reports/allTests")
    def projectsWithResults = subprojects.findAll { subproject ->
        def testResultsDir = new File(subproject.buildDir, "test-results")
        if (!testResultsDir.exists()) return false
        def xmlFiles = subproject.fileTree(testResultsDir).include("**/*.xml")
        return !xmlFiles.empty
    }
    def projectsWithTests = subprojects.findAll { it.tasks.findByName('test') != null }
    testResults.from = projectsWithResults.test
}
